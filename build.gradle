plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'dev.etimbuk'
version = '0.0.1'
description = 'Spring Data JPA: Search'

// Properties
def versions = [
        'springframework' : '3.5.5',
        'spring.dependency-management' : '1.1.7',
        'mapstruct'   : '1.6.3',
        'postgres'  : '42.7.7',
        'log4j' : '2.20.0',
        'lombok'    : '1.18.42',
        'junit5' : '5.9.1',
        'datasource_proxy': '1.12.0'
]

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.mapstruct:mapstruct:${versions.mapstruct}"
    implementation "com.github.gavlyukovskiy:datasource-proxy-spring-boot-starter:${versions.datasource_proxy}"

	compileOnly "org.projectlombok:lombok:${versions.lombok}"
	runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${versions.mapstruct}"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-Amapstruct.verbose=true',
            '-Amapstruct.defaultComponentModel=spring'
    ]
}
